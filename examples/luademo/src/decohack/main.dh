#include <dsdhacked>
#include <friendly>

custom thing pointer boom A_FastChase()
custom thing pointer boom A_Leap()
custom thing pointer boom A_CustomBruisAttack()
custom thing pointer mbf21 A_HardJump(state)
custom thing pointer mbf21 A_CustomVileChase(state)
custom thing pointer boom A_VileSelfHeal()
custom thing pointer boom A_CustomVileTarget()
custom thing pointer boom A_CustomVileAttack()
custom weapon pointer boom A_PlayerHop()
custom weapon pointer boom A_SuperPunch()

thing MTF_ZOMBIEMAN free states
thing MTF_ZOMBIEMAN
{
	clear states
	States
	{
	Spawn:
		POSS AB 10 A_Look
		Loop
		See:
		POSS AABBCCDD 4 A_FastChase
		Loop
	Missile:
		POSS E 10 A_FaceTarget
		POSS F 8 A_PosAttack
		POSS E 8
		Goto See
	Pain:
		POSS G 3
		POSS G 3 A_Pain
		Goto See
	Death:
		POSS H 5
		POSS I 5 A_Scream
		POSS J 5 A_Fall
		POSS K 5
		POSS L -1
		Stop
	XDeath:
		POSS M 5
		POSS N 5 A_XScream
		POSS O 5 A_Fall
		POSS PQRST 5
		POSS U -1
		Stop
	Raise:
		POSS K 5
		POSS JIH 5
		Goto See
	}
}

thing MTF_DEMON free states
thing MTF_DEMON
{
	clear states
	States
	{
	Spawn:
		SARG AB 10 A_Look
		Loop
	See:
		SARG AABBCCDD 2 Fast A_Chase
		Loop
	Missile:
		SARG A 2 Fast A_FaceTarget
		SARG A 0 A_JumpIfTargetCloser("See", 80.0)
		SARG A 8 Fast
		SARG A 0 A_JumpIfTargetCloser("See", 70.0)
		SARG A 14 A_Leap
		Goto See
	Melee:
		SARG EF 8 Fast A_FaceTarget
		SARG G 8 Fast A_SargAttack
		Goto See
	Pain:
		SARG H 2 Fast
		SARG H 2 Fast A_Pain
		Goto See
	Death:
		SARG I 8
		SARG J 8 A_Scream
		SARG K 4
		SARG L 4 A_Fall
		SARG M 4
		SARG N -1
		Stop
	Raise:
		SARG N 5
		SARG MLKJI 5
		Goto See
	}
}

thing MTF_BARON free states
thing MTF_BARON
{
	clear states
	States
	{
	Spawn:
		BOSS AB 10 A_Look
		Loop
	See:
		BOSS AABBCCDD 3 A_Chase
		Loop
	Melee:
	Missile:
		BOSS EF 6 A_FaceTarget
		BOSS G 6 A_CustomBruisAttack
		BOSS G 0 A_HardJump("Hard")
		Goto See
	Hard:
		BOSS EF 6 A_FaceTarget
		BOSS G 6 A_CustomBruisAttack
		Goto See
	Pain:
		BOSS H 2
		BOSS H 2 A_Pain
		Goto See
	Death:
		BOSS I 8
		BOSS J 8 A_Scream
		BOSS K 8
		BOSS L 8 A_Fall
		BOSS MN 8
		BOSS O -1 A_BossDeath
		Stop
	Raise:
		BOSS O 8
		BOSS NMLKJI 8
		Goto See
	}
}

thing MTF_ARCHVILE free states
thing MTF_ARCHVILE
{
	clear states
	States
	{
	Spawn:
		VILE AB 10 A_Look
		Loop
	See:
		VILE AABBCCDDEEFF 2 A_CustomVileChase("SelfHeal")
		Loop
	Missile:
		VILE G 0 Bright A_VileStart
		VILE G 10 Bright A_FaceTarget
		VILE H 8 Bright A_CustomVileTarget
		VILE IJKLMN 8 Bright A_CustomVileTarget
		VILE O 8 Bright A_CustomVileAttack
		VILE P 20 Bright
		Goto See
	SelfHeal:
		VILE GHIHGGG 2 A_VileSelfHeal
		Loop
	Pain:
		VILE Q 5   
		VILE Q 5 A_Pain
		Goto See
	Death:
		VILE Q 7
		VILE R 7 A_Scream
		VILE S 7 A_Fall
		VILE TUVW 7
		VILE XY 5
		VILE Z -1
		Stop
	}
}

thing MTF_EXTRA00 : thing MTF_REVENANT_ROCKET "Super Punch Missile"
{
	clear states
	States
	{
	Spawn:
		FATB A 0 A_FindTracer(90.0)
		FATB AB 2 BRIGHT A_Tracer
		Loop
	Death:
		FBXP A 8 BRIGHT
		FBXP B 6 BRIGHT
		FBXP C 4 BRIGHT
		Stop
	}
}

thing MTF_EXTRA01 : thing MTF_BARON_FIREBALL "Silent Baron Fireball"
{
	seesound ""
}

thing MTF_EXTRA02 "Healing Pod"
{
	EdNum 20000
	Radius 32
	Height 32
	States
	{
	Spawn:
		TNT1 A -1
		Stop
	}
}

weapon Chainsaw free states
weapon Chainsaw
{
	+SILENT
	clear states
	States
	{
	Ready:
		SAWG C 1 A_WeaponReady
		Loop
	Deselect:
		SAWG C 1 A_Lower
		Loop
	Select:
		SAWG C 1 A_Raise
		Loop
	Fire:
		SAWG A 5 A_PlayerHop
		SAWG B 20
		SAWG A 0 A_ReFire
		Goto Ready
	}
}

weapon Fist free states
weapon Fist
{
	clear states
	States
	{
	Ready:
		PUNG A 1 A_WeaponReady
		Loop
	Deselect:
		PUNG A 1 A_Lower
		Loop
	Select:
		PUNG A 1 A_Raise
		Loop
	Fire:
		PUNG B 4
		PUNG C 4 A_SuperPunch
		PUNG D 5
		PUNG C 4
		PUNG B 5 A_ReFire
		Goto Ready
	}
}

strings
{
	STR_GOTCHAINSAW "You got the... trampoline?!"
	STR_GOTSUIT "Rad Suit! Try your weapons"
}
